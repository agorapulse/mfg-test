plugins {
    id "com.github.johnrengelman.shadow" version "5.0.0"
    id "jp.classmethod.aws.lambda" version "0.39"
    id "net.ltgt.apt-eclipse" version "0.21"
    id "groovy"
    id "application"
    id "codenarc"
    id "checkstyle"
}



version "0.1"
group "com.agorapulse"

repositories {
    mavenCentral()
    maven { url "https://jcenter.bintray.com" }
    maven { url "https://dl.bintray.com/agorapulse/libs" }
    maven { url "https://repo.grails.org/grails/core" }
}

configurations {
    // for dependencies that are needed for development only
    developmentOnly 
}

dependencies {
    // custom dependencies
    
    compileOnly "jakarta.persistence:jakarta.persistence-api:2.2.2" 
    compileOnly "com.agorapulse:remember:0.4" 
    implementation "io.micronaut:micronaut-http-client" 
    implementation "com.agorapulse:micronaut-aws-sdk-ag-ws:$micronautLibrariesVersion" 
    implementation "com.agorapulse:micronaut-aws-sdk-cloudwatch:$micronautLibrariesVersion" 
    implementation "com.agorapulse:micronaut-aws-sdk-core:$micronautLibrariesVersion" 
    implementation "com.agorapulse:micronaut-aws-sdk-dynamodb:$micronautLibrariesVersion" 
    implementation "com.agorapulse:micronaut-aws-sdk-kinesis:$micronautLibrariesVersion" 
    implementation "com.agorapulse:micronaut-aws-sdk-kinesis-worker:$micronautLibrariesVersion" 
    implementation "com.agorapulse:micronaut-aws-sdk-s3:$micronautLibrariesVersion" 
    implementation "com.agorapulse:micronaut-aws-sdk-ses:$micronautLibrariesVersion" 
    implementation "com.agorapulse:micronaut-aws-sdk-sns:$micronautLibrariesVersion" 
    implementation "com.agorapulse:micronaut-aws-sdk-sqs:$micronautLibrariesVersion" 
    implementation "com.agorapulse:micronaut-aws-sdk-sts:$micronautLibrariesVersion" 
    implementation "io.micronaut.data:micronaut-data-jdbc:$micronautDataVersion" 
    implementation "io.micronaut.configuration:micronaut-hibernate-gorm" 
    implementation "com.agorapulse:micronaut-facebook-sdk:$micronautFacebookSdkVersion" 
    implementation "com.agorapulse:micronaut-snitch:0.1.0" 
    implementation "org.postgresql:postgresql:42.2.9" 
    implementation "mysql:mysql-connector-java:8.0.18" 
    runtime "com.h2database:h2" 
    runtime "io.micronaut.configuration:micronaut-jdbc-hikari" 
    runtime "org.apache.tomcat:tomcat-jdbc" 
    testImplementation "com.agorapulse.testing:fixt:0.1.3" 
    testImplementation "com.agorapulse:gru-http:$gruVersion" 
    testImplementation "com.agorapulse:gru-api-gateway:$gruVersion" 
    testImplementation "com.stehno.ersatz:ersatz:1.9.0" 
    testImplementation "io.undertow:undertow-core:2.0.13.Final" 
    testImplementation "com.agorapulse:dru-client-dynamodb:$druVersion" 
    testImplementation "com.agorapulse:dru-parser-json:$druVersion" 
    testImplementation "com.agorapulse:dru-client-gorm:$druVersion" 
    testImplementation "org.testcontainers:spock:$testcontainersVersion" 
    testImplementation "org.testcontainers:localstack:$testcontainersVersion" 
    testImplementation "org.testcontainers:postgresql:$testcontainersVersion" 
    testImplementation "org.testcontainers:mysql:$testcontainersVersion" 

    // default dependencies
    annotationProcessor platform("io.micronaut:micronaut-bom:" + micronautVersion)
    annotationProcessor "io.micronaut:micronaut-inject-java"
    annotationProcessor "io.micronaut:micronaut-validation"

    implementation platform("io.micronaut:micronaut-bom:" + micronautVersion)
    implementation "io.micronaut:micronaut-inject"
    implementation "io.micronaut:micronaut-validation"
    implementation "io.micronaut:micronaut-runtime"
    implementation "io.micronaut:micronaut-function-aws"
    implementation "io.micronaut:micronaut-runtime-groovy"

    developmentOnly platform("io.micronaut:micronaut-bom:" + micronautVersion)
    developmentOnly "io.micronaut:micronaut-http-server-netty"
    developmentOnly "io.micronaut:micronaut-function-web"

    compileOnly "io.micronaut:micronaut-inject-groovy"

    implementation 'com.amazonaws:aws-lambda-java-core:' + awsLambdaCoreVersion
    implementation 'com.amazonaws:aws-lambda-java-events:' + awsLambdaEventsVersion

    implementation "com.agorapulse:micronaut-log4aws:1.0.0"

    testAnnotationProcessor platform('io.micronaut:micronaut-bom:' + micronautVersion)
    testAnnotationProcessor "io.micronaut:micronaut-inject-java"

    testImplementation platform('io.micronaut:micronaut-bom:' + micronautVersion)
    testImplementation("org.spockframework:spock-core") {
        exclude group: "org.codehaus.groovy", module: "groovy-all"
    }
    testImplementation 'net.bytebuddy:byte-buddy:1.10.6'

    testImplementation "io.micronaut:micronaut-inject-groovy"
    testImplementation "io.micronaut.test:micronaut-test-spock"
    testImplementation "io.micronaut:micronaut-function-client"
}

test.classpath += configurations.developmentOnly

shadowJar {
    transform(com.github.jengelman.gradle.plugins.shadow.transformers.Log4j2PluginsCacheFileTransformer)
}

tasks.withType(JavaCompile){
    options.encoding = "UTF-8"
    options.compilerArgs.add('-parameters')
}

tasks.withType(GroovyCompile) {
    groovyOptions.forkOptions.jvmArgs.add('-Dgroovy.parameters=true')
}

tasks.withType(Test){
    environment 'TEST_RESOURCES_FOLDER', new File(project.projectDir, 'src/test/resources').canonicalPath
    environment 'AWS_CBOR_DISABLE', 'true'

    systemProperty 'user.timezone', 'UTC'
    systemProperty 'user.language', 'en'
}

checkstyle {
    toolVersion = '8.27'
}

codenarc {
    toolVersion = '1.5'
}

shadowJar {
    mergeServiceFiles()
}

run.classpath += configurations.developmentOnly
run.jvmArgs('-noverify', '-XX:TieredStopAtLevel=1', '-Dcom.sun.management.jmxremote', '-Dmicronaut.environments=dev')
mainClassName = "com.agorapulse.mfg.test.Application"
applicationDefaultJvmArgs = [""]

jar {
    manifest {
        attributes 'Main-Class': mainClassName
    }
}


aws {
    region = 'eu-west-1'
    profileName = 'beta'
}


task deploy(type: jp.classmethod.aws.gradle.lambda.AWSLambdaMigrateFunctionTask, dependsOn: shadowJar) {
    functionName = "MfgTest"
    handler = "com.agorapulse.mfg.test.MfgTestHandler::apply"
    role = 'arn:aws:iam::' + aws.accountId + ':role/lambda_basic_execution'
    runtime = com.amazonaws.services.lambda.model.Runtime.Java8
    zipFile = shadowJar.archivePath
    memorySize = 512
    timeout = 120
}
